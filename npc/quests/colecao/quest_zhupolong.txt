//===== rAthena Script =======================================
//= Quests de Experiência repetitivas do iRO.
//===== By: ==================================================
//= Masao
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= Cronus GIT
//===== Description: =========================================
//= Versão Customizada.
//= Quest repetitível : Matar 50 Zhu Po Long.
//===== Additional Comments: =================================
//= 1.0 Primeira versão.
//= 1.1 Traduzido. [Nacib]
//============================================================
lou_dun02,161,264,4	script	Caçador de Dragões	732,{
 
        if (checkquest(60182) != -1) {
                if (checkquest(60182,HUNTING) != 2 ) {
                        mes "[Caçador de Dragões]";
                        mes "Você ja terminou de caçar 50 Zhu Po Longs?";
                        next;
                        switch(select("Sim:Não:Sair")) {
                        case 1:
                                mes "[Caçador de Dragões]";
                                mes "Hmm, eu não acho que você tenha caçado 50 ainda.";
                                close;
                        case 2:
                                mes "[Caçador de Dragões]";
                                mes "Lembre, preciso de ajuda para caçar 50 Zhu Po Longs.";
                                close;
                        case 3:
                                mes "[Caçador de Dragões]";
                                mes "Tem certeza de que deseja parar a caça?";
                                mes "Qualquer progresso que você fez será apagado.";
                                next;
                                switch(select("Sim:Não")) {
                                case 1:
                                        mes "[Caçador de Dragões]";
                                        mes "Ok então, volte aqui se mudar de ideia.";
                                        erasequest 60182;
                                        close;
                                case 2:
                                        mes "[Caçador de Dragões]";
                                        mes "Mate 50 Zhu Po Longs para tornar este um lugar onde pessoas ganham experiência.";
                                        close;
                                }
                        }
                }
                mes "[Caçador de Dragões]";
                mes "Incrível, você fez isso rapido.";
                getexp 151300,60520;
                erasequest 60182;
                close;
        }
        if ((BaseLevel > 49) && (BaseLevel < 91)) {
                mes "[Caçador de Dragões]";
                mes "Oh ótimo!";
                mes "Você está aqui para me ajudar, certo?";
                mes "Se não, eu não sei o que fazer com os monstros neste lugar!";
                next;
                switch(select("Sim:Não")) {
                case 1:
                        mes "[Caçador de Dragões]";
                        mes "Ótimo! Eu sabia no momento que te vi que eu poderia contar com você!";
                        mes "Eu preciso de você para reduzir a quantidade de monstros que atualmente estão furiosos neste mapa!";
                        mes "Há muitos Zhu Po Long ao redor, o que torna impossível para os jogadores de níveis inferiores ganharem uma boa experiência aqui.";
                        next;
                        mes "[Caçador de Dragões]";
                        mes "Então, você ainda gostaria de me ajudar?";
                        next;
                        switch(select("Sim:Cancelar")) {
                        case 1:
                                if(BaseLevel > 90){
                                        mes "[Caçador de Dragões]";
                                        mes "Obrigado por sua grande ajuda com o Zhu Po Long!";
                                        mes "Atualmente, a situação é estável e não estou precisando de sua ajuda mais para matá-los.";
                                        close;
                                }
                                mes "[Caçador de Dragões]";
                                mes "Mate 50 daqueles Zhu Po Long e deixe eu saber que você está fazendo. Irá recompensá-lo por seus esforços";
                                setquest 60182;
                                close;
                        case 2:
                                mes "[Caçador de Dragões]";
                                mes "Se você mudar de idéia, por favor, volte.";
                                close;
                        }
                
                }
        }
        mes "[Caçador de Dragões]";
        mes "Oh, cara. oh, cara!";
        next;
        mes "[Caçador de Dragões]";
        mes "Desculpe eu não posso falar direito, agora estou em um mundo de dor aqui, estes malditos Zhu Po Longs vão me matar.";
        close;
}